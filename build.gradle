subprojects {
  apply plugin: 'eclipse'
  apply plugin: 'idea'
  apply plugin: 'maven-publish'

  group = 'com.marklogic'
  repositories {
    mavenCentral()
    maven {url "http://developer.marklogic.com/maven2/"}
  }

  ext {
    def timestamp = new java.text.SimpleDateFormat("yyyyMMddHHmmss").format(new Date())
    pubVersion = project.hasProperty("pubVersion") ? project.getProperty("pubVersion") : "0.SNAPSHOT." + timestamp
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        groupId 'com.marklogic'
        artifactId getProject().getName()
        version pubVersion
        from components.java
      }
    }
    repositories {
      maven {
        if (project.hasProperty("nexusPublish")) {
          url "file:///usr/local/sonatype-work/nexus/storage/snapshots"
        } else if (!pubVersion.contains("SNAPSHOT")) {
          url "file://$projectDir/../../gh-pages-marklogic-java/releases"
        }
        else {
          url mavenLocal().url
        }
      }
    }
  }
}

project(":ml-java") {
  apply plugin: 'java'
}

project(":ml-junit") {
  apply plugin: 'java'
  dependencies {
    compile project(":ml-java")
  }
}

project(":ml-gradle") {
  apply plugin: 'groovy'
  dependencies {
    compile project(":ml-java")
    testCompile project(":ml-junit")
  }
}
